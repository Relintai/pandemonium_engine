#!/usr/bin/env python

Import("env")

iphone_lib = [
    "pandemonium_iphone.mm",
    "os_iphone.mm",
    "main.m",
    "app_delegate.mm",
    "view_controller.mm",
    "ios.mm",
    "joypad_iphone.mm",
    "pandemonium_view.mm",
    "tts_ios.mm",
    "display_layer.mm",
    "pandemonium_app_delegate.m",
    "pandemonium_view_renderer.mm",
    "device_metrics.m",
    "keyboard_input_view.mm",
    "native_video_view.m",
]

env_ios = env.Clone()
ios_lib = env_ios.add_library("iphone", iphone_lib)

# (iOS) Enable module support
env_ios.Append(CCFLAGS=["-fmodules", "-fcxx-modules"])


def combine_libs(target=None, source=None, env=None):
    lib_path = target[0].srcnode().abspath
    if "osxcross" in env:
        libtool = "$IPHONEPATH/usr/bin/${ios_triple}libtool"
    else:
        libtool = "$IPHONEPATH/usr/bin/libtool"
    env.Execute(
        libtool + ' -static -o "' + lib_path + '" ' + " ".join([('"' + lib.srcnode().abspath + '"') for lib in source])
    )


combine_command = env_ios.Command("#bin/libpandemonium" + env_ios["LIBSUFFIX"], [ios_lib] + env_ios["LIBS"], combine_libs)
